INCLUDE "Hardware.inc"
INCLUDE "rgbgrafx/rgbgrafx.inc"

Section "Credits Logo Data",DATA
INCLUDE "imagedata/credits.inc"

Section "Credits scene",CODE
scene_credits::
  di
  call initialize_interrupts
  ei
  ; Set all palette colors to black for the duration of loading image data
  RGBG_WaitForVRAM
  ld A, $FF
  ld [rBGP], A

  ; Set LCD control parameters
  ld A, (LCDCF_ON | LCDCF_WINOFF | LCDCF_BGON | LCDCF_BG9800 | LCDCF_BG8000)
  ld [rLCDC], A

  ; Initialize high byte of the tileset starting address
  ld A, $80
  ld [RGBG_tileset], A
  ; Load tile patterns to VRAM
  ld BC, credits_tile_data
  ld D, $FF
  ld E, 0
  RGBG_WaitForVRAM
  call RGBG_LoadTiles

  ; Initialize high byte of the tilemap starting address
  ld A, $98
  ld [RGBG_tilemap], A

  ; Set tilemap offset to zero
  xor A
  ld [RGBG_map_offset], A

  ; Load tile map
  RGBG_WaitForVRAM
  ld BC, credits_map_data
  ld H, $0
  ld L, $0
  ld E, credits_tile_map_width
  ld D, credits_tile_map_height
  call RGBG_SetTileMap

  ; Reset background scroll registers
  xor A
  ld [rSCX], A
  ld [rSCY], A

  ; Reset color palette to default
  RGBG_WaitForVRAM
  ld A, %11100100
  ld [rBGP], A
  ld [rOBP0], A
  ld [rOBP1], A

.loop:
  halt
  jr nz, .loop

  ret
