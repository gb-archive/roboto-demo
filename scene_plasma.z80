INCLUDE "Hardware.inc"
INCLUDE "rgbgrafx/rgbgrafx.inc"

; $FF90 -> is free

Section "Plasma scene",CODE
scene_plasma::
  ; Set all palette colors to black for the duration of scene setup
  RGBG_WaitForVRAM
  ld A, $FF
  ld [rBGP], A

  ; Reset background scroll registers
  xor A
  ld [rSCX], A
  ld [rSCY], A

  ; Set LCDC register initial values
  ld A, (LCDCF_ON | LCDCF_WINOFF | LCDCF_BG8000 | LCDCF_BG9800 | LCDCF_BGON)
  ld [rLCDC], A

  di
  ; Clear interrupt handlers
  call initialize_interrupts

  ; Load tiles into VRAM
  call load_tiles

  ; Initialize buffer used for plasma calculations
  call init_plasma_buffer

  ; Set new V-Blank handler
  ld BC, plasma_vblank
  call set_vblank_interrupt

  ; Reset color palette to default
  RGBG_WaitForVRAM
  ld A, %11100100
  ld [rBGP],A
  ld [rOBP0],A
  ld [rOBP1],A
  ei

.loop:
  halt
  jr .loop
  ret

plasma_vblank:
  ld HL, $9800
  call write_plasma_to_vram
  ret

init_plasma_buffer:
  ; Initialize plasma buffer to zero.
  ld D, $FF
  ld HL, plasma_buffer
  xor A
.loop:
  ldi [HL], A
  dec D
  jr nz, .loop

  ld D, $69
.loop2:
  ldi [HL], A
  dec D
  jr nz, .loop2
  ret

write_plasma_to_vram:
  ; Code writes 18 lines (tiles 0-19) to [HL] from plasma_data
  ld DE, plasma_buffer
  ld B, 18 ; Row counter
.row_loop:
  push BC
  ld B, 10 ; Run 10 times, since inner loop does two iterations (=20 tiles)
.tile_loop:
  RGBG_WaitForVRAM ; Wait for VRAM to become available
  ld A, [DE]
  ldi [HL], A
  inc DE

  ld A, [DE]
  ldi [HL], A
  inc DE

  dec B ; Decrement column counter
  jr nz, .tile_loop
  ld BC, $000C ; Skip $C non-visible tiles on row
  add HL, BC
  pop BC
  dec B ; Decrement row counter
  jr nz, .row_loop
  ret

load_tiles:
  RGBG_WaitForVRAM
  ld A, $80
  ld [RGBG_tileset], A
  ld BC, plasma_tiles ; Plasma tile data address
  ld D, 64 ; Number of tiles
  ld E, 0 ; Offset required by RGBG
  call RGBG_LoadTiles
  ret

INCLUDE "imagedata/plasma_tiles.inc"

Section "Plasma variables",BSS
plasma_buffer: DS 360
